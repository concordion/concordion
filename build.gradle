buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'net.researchgate:gradle-release:2.0.2'
        classpath 'org.ajoberstar:gradle-git:1.3.0'
    }
}

apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'net.researchgate.release'
apply plugin: 'wrapper'
apply plugin: 'org.ajoberstar.github-pages'

wrapper {
    gradleVersion = "2.7"
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'junit:junit:4.12',
            'ognl:ognl:2.6.9'

    compile ('xom:xom:1.2.5') {
        exclude group: 'xalan', module: 'xalan'
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    
    testCompile 'org.concordion:concordion-embed-extension:1.1.2'
}

sourceSets {
    testDummies {  // "crash test dummies" - specs+fixtures that are used by the tests but aren't run as tests themselves
        java {
            srcDir 'src/test-dummies/java'
        }
        resources {
            srcDir 'src/test-dummies/resources'
        }
        compileClasspath = sourceSets.main.output + configurations.compile
    }

    test {
        runtimeClasspath += sourceSets.testDummies.output
    }
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

test {
    systemProperties['concordion.output.dir'] = "${reporting.baseDir}"
}

compileTestJava.dependsOn compileTestDummiesJava

distributions {
    version = version - "-SNAPSHOT"
    main {
        contents {
            from ('.') { include '*.txt','build.gradle','src/**' }
            into ('lib') {
                from 'licenses'
            }
        }
    }
}

// 'gradle release' will also upload release to Nexus
createReleaseTag.dependsOn uploadArchives
release.dependsOn clean

extraArchive {
    tests = true    // create tests jar file and upload
}

modifyPom {
    project {
        name 'Concordion'
        description 'Concordion is an open source framework for Java that lets you turn a plain English description of a requirement into an automated test.'
        url 'http://www.concordion.org'
        packaging 'jar'
        inceptionYear '2008'

        scm {
            url 'https://github.com/concordion/concordion.git'
            connection 'scm:git:git://github.com/concordion/concordion.git'
            developerConnection 'scm:git:git@github.com:concordion/concordion.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                distribution 'repo'
            }
        }

        issueManagement{
            system 'GoogleCode'
            url 'http://code.google.com/p/concordion/issues/list'
        }

        developers {
            developer {
                id 'peterson3'
                name 'David Peterson'
                roles { role 'Project Creator' }
            }
            developer {
                id 'nigel.charman.nz'
                name 'Nigel Charman'
                roles  { role 'Project Lead' }
            }
            developer {
                id 'drtimwright'
                name 'Tim Wright'
                roles  { role 'Developer' }
            }
            developer {
                id 'jose.m.beas'
                name 'Jose M Beas'
                roles  { role 'Developer' }
            }
            developer {
                id 'habuma'
                roles  { role 'Developer' }
            }
            developer {
                id 'pinfieldharm'
                roles  { role 'Developer' }
            }
            developer {
                id 'wangyizhuo'
                roles  { role 'Developer' }
            }
        }
    }
}

githubPages {
    repoUri = 'git@github.com:concordion/concordion.git'
//    deleteExistingFiles = false
    pages {
        from 'ghpages'
        
        into ('latest') {
            from ('build/reports/spec/concordion') {
                into 'spec'
            }
            
            from ('build/docs/javadoc/') {
                into 'javadoc'
            }
        }
    }  
}

prepareGhPages.dependsOn test
prepareGhPages.dependsOn javadoc