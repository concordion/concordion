buildscript {
    ext {
        junitPlatformVersion = '1.0.0-M4'
        junitJupiterVersion = '5.0.0-M4'
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
    }
}

apply plugin: 'java'
apply plugin: 'wrapper'
apply plugin: 'org.junit.platform.gradle.plugin'

description = 'Adds support for JUnit 5.'

wrapper {
    // If changing this, you may need to update the distributionUrl to http rather than https in gradle/wrapper/gradle-wrapper.properties to workaround a TravisCI with openjdk6. The other fixes in http://stackoverflow.com/questions/29584328/gradlew-bat-and-gradlew-sslhandshakeexception#answer-30924895 didn't work for me.
    gradleVersion = "2.9"
}

repositories {
    jcenter()
}

dependencies {
    compile project(':concordion-base')

    compile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    compile "org.junit.platform:junit-platform-engine:${junitPlatformVersion}"
    compile "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
    compile "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"

    testRuntime "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntime "org.junit.platform:junit-platform-engine:${junitPlatformVersion}"
    testRuntime "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

junitPlatform {
    filters {
        engines {
            include 'junit-jupiter'
            exclude 'junit-vintage'
        }
    }
}

sourceSets {
    testDummies {  // "crash test dummies" - specs+fixtures that are used by the tests but aren't run as tests themselves
        java {
            srcDir 'src/test-dummies/java'
        }
        resources {
            srcDir 'src/test-dummies/resources'
        }
        compileClasspath = sourceSets.main.output + configurations.compile
    }

    test {
        compileClasspath += sourceSets.testDummies.output
        runtimeClasspath += sourceSets.testDummies.output
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

test {
    systemProperties['concordion.output.dir'] = "${reporting.baseDir}"
}

compileTestJava.dependsOn compileTestDummiesJava
